SET STATEMENT_GROUP = "Group 2"

SET TextLocation = Abstract
SET Evidence = "Phosphorylation at Ser473, along with Thr308 of its activation loop, is deemed necessary
for Akt function, although the regulatory mechanisms and physiological importance
of each phosphorylation site remain to be fully understood."
SET Citation = {"PubMed","Cell","16962653","2006-10-07","Jacinto E|Facchinetti V|Liu D|Soto N|Wei S|Jung SY|Huang Q|Qin J|Su B",""}
SET Species = 9606

p(HGNC:AKT1,pmod(P,T,308)) directlyIncreases kin(p(HGNC:AKT1))
p(HGNC:AKT1,pmod(P,S,473)) directlyIncreases kin(p(HGNC:AKT1))

SET Evidence = "Genetic ablation of sin1 abolished Akt-Ser473 phosphorylation and disrupted rictor-mTOR
interaction but maintained Thr308 phosphorylation."

p(HGNC:MAPKAP1) increases p(HGNC:AKT1,pmod(P,S,473)) //sin1 = MAPKAP1

SET Cell = fibroblast
SET TextLocation = Results
SET Evidence = "We next examined the Akt T-loop Thr308 phosphorylation in wild-type and SIN1−/− cells.
We found that although Ser473 phosphorylation was completely abolished in
the SIN1−/− cells, Thr308 phosphorylation of Akt was not blocked (Figure 3A)."
SET Species = 10090

p(MGI:Mapkap1) increases p(MGI:Akt1,pmod(P,S,473))
p(MGI:Mapkap1) causesNoChange p(MGI:Akt1,pmod(P,S,308))

SET Evidence = "We also examined phosphorylation of TSC2, another Akt target (Manning et al., 2002),
at the Akt target sites Ser939 and Thr1462 and found no significant difference
between wild-type and SIN1−/− cells upon serum and insulin stimulation (Figure
3C)."

kin(p(MGI:Akt1)) directlyIncreases p(MGI:Tsc2,pmod(P,S,939))
kin(p(MGI:Akt1)) directlyIncreases p(MGI:Tsc2,pmod(P,T,1462)) //Human T1462 = Mouse T1465

SET Evidence = "To identify a function that could be linked specifically to Akt-Ser473 phosphorylation,
we further examined known Akt substrates that may have defective phosphorylation
in SIN1−/− cells. We found that phosphorylation of FoxO1/3a (also called
FKHR/FKHRL1) (Greer and Brunet, 2005), was affected in SIN1−/− cells. In particular,
phosphorylation of FoxO1/3a at Thr24/Thr32 was significantly decreased in
the absence of SIN1 under normal growing and restimulated conditions (Figure 4A)."

p(MGI:Mapkap1) increases p(MGI:Foxo1,pmod(P,T,24))
p(MGI:Mapkap1) increases p(MGI:Foxo3,pmod(P,T,32))
kin(p(MGI:Akt1)) directlyIncreases p(MGI:Foxo1,pmod(P,T,24))
kin(p(MGI:Akt1)) directlyIncreases p(MGI:Foxo3,pmod(P,T,32))

SET Evidence = "We also found that the recombinant FoxO1 could not be phosphorylated at Thr24/32 in
vitro by an Akt mutant harboring an alanine mutation in either Thr308 or Ser473,
whereas a Ser473 to Ala mutated Akt could still phosphorylate recombinant GSK3
(Figure S2). These results indicate that Akt-Ser473 phosphorylation is required
for FoxO1/3a phosphorylation at Thr24/32."

p(MGI:Akt1,pmod(P,S,473)) directlyIncreases p(MGI:Foxo1,pmod(P,T,24))
p(MGI:Akt1,pmod(P,S,473)) directlyIncreases p(MGI:Foxo3,pmod(P,T,32))
kin(p(MGI:Akt1)) directlyIncreases p(MGI:Foxo1,pmod(P,T,24))
kin(p(MGI:Akt1)) directlyIncreases p(MGI:Foxo3,pmod(P,T,32))
p(MGI:Akt1,pmod(P,S,473)) directlyIncreases kin(p(MGI:Akt1))

SET Evidence = "In etoposide-treated cells, there was a 2-fold increase in the number of cells that
underwent apoptosis after 48 hr in SIN1 knockouts (Figure 4D). Thus, we find
that the SIN1−/− cells were more sensitive to stress-induced apoptosis, suggesting
that Akt-Ser473 phosphorylation plays an important role in cell survival (Figures
4C and 4D)."

p(MGI:Mapkap1) decreases bp(GOBP:"apoptotic process")

SET Evidence = "The phosphorylation of S6K at Thr389, which is the TORC1-mediated site, was not inhibited
in the SIN1-/- cells (Figure 5A)."

kin(complex(SCOMP:"TORC1 Complex")) directlyIncreases p(MGI:Rps6kb1,pmod(P,T,412))

SET Evidence = "Likewise, phosphorylation of another translational regulator, 4E-BP1, at the mTOR
target site Thr37/46 (Gingras et al., 1999), was also not impaired in SIN1−/− cells
(Figure 5A)."

kin(complex(SCOMP:"TORC2 Complex")) directlyIncreases p(MGI:Eif4ebp1,pmod(P,T,36))
kin(complex(SCOMP:"TORC2 Complex")) directlyIncreases p(MGI:Eif4ebp1,pmod(P,T,45))
kin(p(MGI:Mtor)) directlyIncreases p(MGI:Eif4ebp1,pmod(P,T,36))
kin(p(MGI:Mtor)) directlyIncreases p(MGI:Eif4ebp1,pmod(P,T,45))

SET Evidence = "Akt Ser473 was strongly induced in wild-type cells by different growth factors such
as the platelet-derived growth factor, epidermal growth factor, and insulin,
even in the absence of amino acids and glucose (Figure 5B). In SIN1−/− cells, Akt-Ser473
phosphorylation was not induced by any type of stimulus."

p(MGI:Mapkap1) increases p(MGI:Akt1,pmod(P,S,473))
p(SFAM:"PDGF Family") increases p(MGI:Akt1,pmod(P,S,473))
p(MGI:Egf) increases p(MGI:Akt1,pmod(P,S,473))
p(SFAM:"INS Family") increases p(MGI:Akt1,pmod(P,S,473))

SET Evidence = "As shown in Figures 5E and 5F, SIN1 was able to associate with Akt under normal growth,
starvation, or restimulated conditions. Although SIN1 and Akt interacted
under starved condition, the SIN1 bound Akt was not phosphorylated on Ser473 (Figure
5E)."

complex(p(MGI:Akt1),p(MGI:Mapkap1))

SET Evidence = "Akt has been well characterized as a prosurvival molecule, and part of this function
is mediated through its suppression of the activity of the FoxO proteins (Greer
and Brunet, 2005)."

kin(p(MGI:Akt1)) directlyDecreases tscript(p(SFAM:"FOXO Family"))
